// @strict-types

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыОповещения = ПараметрыДобавленияНастройкиПоКлючу();
	Если Копирование Тогда
		ПараметрыОповещения.Копирование = Истина;
		ПараметрыОповещения.Копируемое = Элемент.ТекущиеДанные.КлючСписка;
	КонецЕсли;
	
	ОписаниеОповещения  = Новый ОписаниеОповещения(
		"ДобавитьНастройкуКоКлючу", ЭтотОбъект, ПараметрыОповещения);
	
	ПараметрыОткрытия = ИТМ_АдминистрированиеДинамическихСписковКлиент.ПараметрыОткрытияФормыВыбораКлючаСписка();
	ПараметрыОткрытия.Владелец = ЭтотОбъект;
	ПараметрыОткрытия.Отбор.ЕстьНастройка = Ложь;
	ПараметрыОткрытия.ОписаниеОповещения  = ОписаниеОповещения;
	
	ИТМ_АдминистрированиеДинамическихСписковКлиент.ОткрытьФормуВыбораКлючаСписка(ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НормализоватьНаименованияКлючевыхОпераций(Команда)
	
	НормализоватьНаименованияКлючевыхОперацийНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры добавления настройки по ключу.
// 
// Возвращаемое значение:
//  Структура - Параметры добавления настройки по ключу:
// * Копирование - Булево - Режим копирования
// * Копируемое - Неопределено, СправочникСсылка.КлючевыеОперации - Копируемое значение
&НаКлиенте
Функция ПараметрыДобавленияНастройкиПоКлючу()
	
	Результат = Новый Структура;
	Результат.Вставить("Копирование", Ложь);
	Результат.Вставить("Копируемое" , Неопределено);
	
	Возврат Результат;
	
КонецФункции

// Добавить настройку ко ключу.
// 
// Параметры:
//  РезультатЗакрытия - СправочникСсылка.КлючевыеОперации - Результат закрытия
//  ПараметрыДобавления - см. ПараметрыДобавленияНастройкиПоКлючу
&НаКлиенте
Процедура ДобавитьНастройкуКоКлючу(РезультатЗакрытия, ПараметрыДобавления) Экспорт
	
	Если ТипЗнч(РезультатЗакрытия) <> Тип("СправочникСсылка.КлючевыеОперации") 
		ИЛИ РезультатЗакрытия.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьНастройкуКоКлючуНаСервере(РезультатЗакрытия, ПараметрыДобавления);
	
КонецПроцедуры

// Добавить настройку ко ключу на сервере.
// 
// Параметры:
//  КлючСписка - СправочникСсылка.КлючевыеОперации - Ключ списка
//  ПараметрыДобавления - см. ПараметрыДобавленияНастройкиПоКлючу
&НаСервере
Процедура ДобавитьНастройкуКоКлючуНаСервере(КлючСписка, ПараметрыДобавления)
	
	РегистрыСведений.ИТМ_НастройкиДинамическихСписков.ДобавитьЗапись(КлючСписка);
	
	Если ПараметрыДобавления.Копирование Тогда
		КопируемыеНастройки = РегистрыСведений.ИТМ_НастройкиДинамическихСписков.НастройкиСписка(ПараметрыДобавления.Копируемое);
		РегистрыСведений.ИТМ_НастройкиДинамическихСписков.ДобавитьЗапись(КлючСписка, Ложь, КопируемыеНастройки);
	КонецЕсли;
	
	ДанныеКлючаЗаписи = Новый Структура("КлючСписка", КлючСписка);
	КлючТекущейЗаписи = РегистрыСведений.ИТМ_НастройкиДинамическихСписков.СоздатьКлючЗаписи(ДанныеКлючаЗаписи);
	
	Элементы.Список.ТекущаяСтрока = КлючТекущейЗаписи;
	
КонецПроцедуры

&НаСервере
Процедура НормализоватьНаименованияКлючевыхОперацийНаСервере()
	
    ИТМ_АдминистрированиеДинамическихСписков.НормализоватьНаименованияКлючевыхОпераций();
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
