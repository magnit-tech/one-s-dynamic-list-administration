// @strict-types

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВычисляемыеДанные(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолноеИмяФормыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОткрываемойФормы = "РегистрСведений.ИТМ_НастройкиДинамическихСписков.Форма.ФормаВыбораФормы";
	ПараметрыОткрытия   = Новый Структура("ПолноеИмяФормы", ПолноеИмяФормы);
	ОткрытьФорму(ИмяОткрываемойФормы,ПараметрыОткрытия,Элементы.ПолноеИмяФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеИмяФормыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПолноеИмяФормы = Строка(ВыбранноеЗначение);
	ОбновитьВычисляемыеДанные(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяЭлементаСпискаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьВыборЭлементаСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеИмяФормыПриИзменении(Элемент)
	
	ОбновитьВычисляемыеДанные(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяЭлементаСпискаПриИзменении(Элемент)
	
	ОбновитьВычисляемыеДанные(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Команда_СоздатьЭлемент(Команда)
	
	КлючеваяОперация = ЭлементКлючевойОперации(КлючеваяСтрока);
	ОповеститьОВыборе(КлючеваяОперация);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВыборЭлементаСписка(РезультатВопроса = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПоказатьВыборЭлементаСписка", ЭтотОбъект, ДополнительныеПараметры), 
			"Для поиска элементов форма будет программно получена. Продолжить?", РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		//@skip-check use-non-recommended-method - нет необходимости в открытии формы для получения списка элементов
		ВыбраннаяФорма   = ПолучитьФорму(ПолноеИмяФормы); //sonar:DeprecatedMethodGetForm
		ЭлементыТаблицы  = ИТМ_АдминистрированиеДинамическихСписковКлиентСервер.ЭлементыТаблицыНаФорме(ВыбраннаяФорма.Элементы);
		ОповещениеВыбора = Новый ОписаниеОповещения("ОбработатьВыборИмениЭлементаСписка", ЭтотОбъект);
		
		ИменаЭлементов = Новый СписокЗначений; //СписокЗначений из Строка
		Для Каждого ЭлементТаблица Из ЭлементыТаблицы Цикл
			ИменаЭлементов.Добавить(ЭлементТаблица.Имя, ЭлементТаблица.Имя);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ИменаЭлементов) Тогда
			ИменаЭлементов.ПоказатьВыборЭлемента(ОповещениеВыбора, "Выберите элемент формы");
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

// Обработать выбор имени элемента списка.
// 
// Параметры:
//  РезультатВыбора - ЭлементСпискаЗначений - Результат выбора
//  ДополнительныеПараметры - Структура - Дополнительные параметры
&НаКлиенте
Процедура ОбработатьВыборИмениЭлементаСписка(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяЭлементаСписка = Строка(РезультатВыбора.Значение);
	
	ОбновитьВычисляемыеДанные(ЭтотОбъект);
	
КонецПроцедуры

// Обновить вычисляемые данные.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьВычисляемыеДанные(Форма)
	
	Форма.КлючеваяСтрока = ИТМ_АдминистрированиеДинамическихСписковКлиентСервер.ИмяКлючаДинамическогоСписка(
		Форма.ПолноеИмяФормы,  Форма.ИмяЭлементаСписка);
	
	Форма.Наименование   = ИТМ_АдминистрированиеДинамическихСписковКлиентСервер.НаименованиеКлючевойОперации(
		Форма.КлючеваяСтрока);
	
КонецПроцедуры

// Создать элемент ключевой операции.
// 
// Параметры:
//  КлючеваяСтрока - Строка - Ключевая строка
// 
// Возвращаемое значение:
//  СправочникСсылка.КлючевыеОперации, Произвольный - Создать элемент ключевой операции
&НаСервереБезКонтекста
Функция ЭлементКлючевойОперации(КлючеваяСтрока)
	
	Возврат ИТМ_АдминистрированиеДинамическихСписков.ПолучитьКлючевуюОперацию(КлючеваяСтрока);
	
КонецФункции

#КонецОбласти

